plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'com'
version = '2.3'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = 'https://repo.codemc.io/repository/maven-public/'
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.13-R0.1-SNAPSHOT"
    implementation 'org.bstats:bstats-bukkit:3.0.2'
    implementation 'org.jodd:jodd-http:6.3.0'
    implementation 'org.json:json:20231013'
}

shadowJar {
    archiveFileName = "MineChatGPT-${project.version}.jar"
    relocate 'jodd', 'com.ddaodan.shaded.jodd'
    relocate 'org.json', 'com.ddaodan.minechatgpt.libs.org.json'
    relocate 'org.bstats', 'com.ddaodan.minechatgpt.libs.org.bstats'
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}
tasks.build.dependsOn shadowJar

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
